uniform float near;
uniform float far;
uniform mat4 projMat;
varying vec3 posEye;
varying float radius;
void main(void) {

    // Calculate normal from texture coordinates
	vec3 N;
	N.xy = gl_TexCoord[0].xy*vec2(2.0, -2.0) + vec2(-1.0, 1.0);
	float mag = dot(N.xy, N.xy);
	if (mag > 1.0) discard; // kill pixels outside circle
	N.z = sqrt(1.0-mag);
	
	// Point on surface of sphere in eye space
	vec4 spherePosEye = vec4(posEye+N*radius, 1.0);
	
	// Calculate depth from sphere eye space
	vec4 clipSpacePos = projMat * spherePosEye;

	// Calculate depth and color
	gl_FragDepth = (clipSpacePos.z / clipSpacePos.w)*0.5+0.5;
	float alpha = exp(-mag*2.0);
	gl_FragData[0] = vec4(N.z*radius*2.0*alpha);
}

